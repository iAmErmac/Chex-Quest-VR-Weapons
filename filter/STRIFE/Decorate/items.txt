//Event based drops

//Rowan reward stuff
Actor RowanRewards : Custominventory
{
	ConversationID 171
	
	States //additional rewards are given when talked to Macil
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("Gold",50)//always give monetary reward	
		//additional rewards given in sequence (last to first):
		TNT1 A 0 A_JumpIfInventory("Communicator",1,"NoMoreReward",AAPTR_PLAYER1)
		TNT1 A 0 A_JumpIfInventory("BeldinsNewRing",1,"GiveReward",AAPTR_PLAYER1)//brought back Beldin's ring
		Goto NoMoreReward
	GiveReward:
		TNT1 A 0 A_TakeInventory("BeldinsNewRing",1)//Remove the ring
		TNT1 A 0 A_GiveInventory("Communicator")//given to be able to hear Blackbird
		TNT1 A 0 A_GiveInventory("QuestItem1")//required for Blackbird's "bridge clue"
		TNT1 A 0 A_GiveInventory("BaseKey")//required for "I need gold" dialog to appear
		Stop
	NoMoreReward: //no more rewards available
		TNT1 A 0
		Stop
	}
}

//Rowan drop stuff to continue progress if Rowan is killed early
Actor RowanDrops : CustomInventory
{
	+FIXMAPTHINGPOS
	Inventory.PickupMessage "$TXT_COMMUNICATOR"
	States
	{
	Spawn:
		TNT1 A 0 //if player already has the comm unit, its not spawned
		TNT1 A 0 A_JumpIfInTargetInventory("Communicator",1,"NoSpawn",AAPTR_PLAYER1)
		TNT1 A 1 A_DropItem("LargeZorcherSpawner")//the only way to obtain it other than with the dialogs
		COMM A -1
		Stop
	Pickup:
		TNT1 A 0 //the items below are needed for story progression:
		TNT1 A 0 A_GiveInventory("Communicator")//given to be able to hear Blackbird
		TNT1 A 0 A_GiveInventory("QuestItem1")//required for Blackbird's "bridge clue"
		TNT1 A 0 A_GiveInventory("BaseKey")//required for "I need gold" dialog to appear
	NoSpawn:
		TNT1 A 0 //create nothing
		Stop
	}
}

//Macil gives additional stuff on meeting
Actor MacilGiftPack : Custominventory
{
	ConversationID 172
	
	States //additional rewards are given when talked to Macil
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("Gold",300)//always give monetary reward	
		TNT1 A 0 A_TakeInventory("StrifeOfferingChalice",1)//Remove the stolen chalice from inventory
		TNT1 A 0 A_TakeInventory("QuestItem2",1)//Remove the alarm given by stolen chalice
		//additional rewards given in sequence (last to first):
		TNT1 A 0 A_JumpIfInventory("QuestItem13",1,"GiveSSG",AAPTR_PLAYER1)//freed the prisoners
		Goto GiveSilencedPistol
	GiveSSG:
		TNT1 A 0 A_JumpIfInventory("NewSuperLargeZorcher",1,"GiveSilencedPistol",AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("NewSuperLargeZorcher",1)
		TNT1 A 0 A_Print("One of the prisoners recovered this fine weapon. Wield it for our cause!")
	GiveSilencedPistol:
		TNT1 A 0 A_JumpIfInventory("SilencedMiniZorcher",1,"NoExtraReward",AAPTR_PLAYER1)
		TNT1 A 0 A_GiveInventory("SilencedMiniZorcher",1)
		TNT1 A 0 A_TakeInventory("NewMiniZorcher",1) //Replaced by the silenced version
		TNT1 A 0 A_Print("Take this silenced gun. Hope this will help on your covert operations!")
	NoExtraReward:
		TNT1 A 0
		Stop
	}
}

//drops chainsaw
Actor BeldinDropItems
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_DropItem("BeldinsNewRing", 1)
		//TNT1 A 1 A_DropItem("SporkSpawner", 1) //Chainsaw is being dropped elsewhere so we don't need this
		Stop
	}
}

Actor ChaliceItemSpawner : CustomInventory
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_SpawnItem("StrifeOfferingChalice")
		TNT1 A 1 A_DropItem("ClipBoxSpawner", 1)
		TNT1 A 1 A_DropItem("ShellBoxSpawner", 1)
		TNT1 AAAAAAAAAA 0 A_Warp(AAPTR_DEFAULT, random(1,4), random(1,4))
		TNT1 A 1 A_DropItem("SporkSpawner")
		Stop
	}
}